
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = НачалоНедели(ТекущаяДатаСеанса());
	КонецПериода = КонецНедели(ТекущаяДатаСеанса());
	
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода,КонецПериода);
	Планировщик.ШкалаВремени.Положение = ПоложениеШкалыВремени.Верх;  
    Планировщик.ВыравниватьГраницыЭлементовПоШкалеВремени = Истина; 

	ОбновитьПланировщик();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиента" Тогда
		ОбновитьПланировщик();
	КонецЕсли
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Специалист = ЗначенияИзмерений.Получить("Специалист");
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ВремяНачалаРаботПлан", Начало);
	ЗначенияЗаполнения.Вставить("ВремяОкончанияРаботПлан", Конец);
	ЗначенияЗаполнения.Вставить("ДатаПроведенияРабот", Начало);
	ЗначенияЗаполнения.Вставить("Специалист", Специалист);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.Форма.ФормаДокумента", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПланировщик()
	
	Планировщик.Элементы.Очистить();
	ИзмеренияПланировщика = Планировщик.Измерения;
	ИзмеренияПланировщика.Очистить();
	
	ИзмерениеСпециалист = ИзмеренияПланировщика.Добавить("Специалист");
 	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист,
		|	ВКМ_ОбслуживаниеКлиента.Специалист.Представление КАК СпециалистПредставление
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот >= &НачалоГода
		|	И ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот <= &КонецГода
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_ОбслуживаниеКлиента.Специалист,
		|	ВКМ_ОбслуживаниеКлиента.Специалист.Представление
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВКМ_ОбслуживаниеКлиента.Ссылка КАК Ссылка,
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот КАК ДатаПроведенияРабот,
		|	ВКМ_ОбслуживаниеКлиента.ВремяНачалаРаботПлан КАК ВремяНачалаРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.ВремяОкончанияРаботПлан КАК ВремяОкончанияРаботПлан,
		|	ВКМ_ОбслуживаниеКлиента.Специалист КАК Специалист
		|ИЗ
		|	Документ.ВКМ_ОбслуживаниеКлиента КАК ВКМ_ОбслуживаниеКлиента
		|ГДЕ
		|	ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот >= &НачалоГода
		|	И ВКМ_ОбслуживаниеКлиента.ДатаПроведенияРабот <= &КонецГода";
	
	Запрос.УстановитьПараметр("КонецГода", КонецГода(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("НачалоГода", НачалоГода(ТекущаяДатаСеанса()));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	ВыборкаСпециалист = МассивРезультатов[0].Выбрать();
	ВыборкаРаботы = МассивРезультатов[1].Выбрать();
	
	Пока ВыборкаСпециалист.Следующий() Цикл
		НовыйСотрудник = ИзмерениеСпециалист.Элементы.Добавить(ВыборкаСпециалист.Специалист);  
	    НовыйСотрудник.Текст = ВыборкаСпециалист.Специалист;
	КонецЦикла;
	
	Пока ВыборкаРаботы.Следующий() Цикл
		ВремяНачалаРабот = ВыборкаРаботы.ДатаПроведенияРабот + (ВыборкаРаботы.ВремяНачалаРаботПлан - Дата(1,1,1));
		ВремяОкончанияРабот = ВыборкаРаботы.ДатаПроведенияРабот + (ВыборкаРаботы.ВремяОкончанияРаботПлан- Дата(1,1,1));
		
	    СоответствиеЗначений = Новый Соответствие;
        СоответствиеЗначений.Вставить("Специалист",   ВыборкаРаботы.Специалист);
        
		ЭлементПланировщика = Планировщик.Элементы.Добавить(ВремяНачалаРабот, ВремяОкончанияРабот);
		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеЗначений);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти